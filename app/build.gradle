import io.github.fvarrui.javapackager.gradle.PackageTask
import io.github.fvarrui.javapackager.model.Platform

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.5.0'
    }
}

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
	id 'eclipse'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

repositories {
    // Use JCenter for resolving dependencies.
    jcenter()
}

dependencies {
    // Use JUnit test framework.
    testImplementation 'junit:junit:4.13'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:29.0-jre'

    // JMetro javafx theme
    implementation 'org.jfxtras:jmetro:11.6.14'

    // MvvmFx framework
    implementation 'de.saxsys:mvvmfx:1.8.0'

    // Guice DI framework
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'com.google.inject:guice:4.2.3'

    // MvvmFx DI support
    implementation 'de.saxsys:mvvmfx-guice:1.8.0'
}

version = file('version').text.trim()

application {
    // Define the main class for the application.
    mainClassName = 'episim.Main'
    // Remove Guice reflective access warnings
    applicationDefaultJvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED']
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

task packageApp(type: PackageTask, dependsOn: build) {
    mainClass = 'episim.Main'
    platform = Platform.auto
    bundleJre = true
    generateInstaller = false
    createZipball = true
    createTarball = true
}

eclipse {
    project {
        name = 'EpiSim'
	}
}
